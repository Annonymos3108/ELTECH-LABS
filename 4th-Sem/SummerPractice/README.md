# Учебная практика. Тема: Основы исследования функциональности программного обеспечения и разработки программного обеспечения на ассемблере

## Задание на практику

Изучить основные инструкции процессора архитектуры IA-32 (x86) и реализовать приложение на языке ассемблера. Объем и функциональность приложения для реализации — по выбору студента. Компилятор ассемблера и операционная система, для которой будет реализовано приложение — по выбору студента.

В рамках данной практики было разработано консольное приложение для работы с матрицами. В программе реализованы инструкции, которые позволяют читать, хранить, сортировать, а также вывести на экран матрицу, размеры которого не превышают 5х5.

## Описание функциональности реализованного приложения

Консольное приложение для работы с матрицами. При запуске программы на экран выводится сообщение, где от пользователя требуется ввести количество строк и столбцов матрицы (макс. 5х5), затем нужно ввести и саму матрицу. 
После получения матрицы программа выводит на экран изначально введенную пользователем и отсортированную по возрастанию элементов матрицы.
Для разработки данного приложения в качестве текстового редактора было выбрано Microsoft Visual Studio Code.</br>
**Компилятор:** Microsoft Macro Assembler 6.14.8444 (MASM).</br>
**Язык программирования:** Assembler (MASM).</br>
**Операционная система:** Microsoft Windows 10.

## Описание инструкций процессора, использованных в реализованном приложении

При запуске программы в памяти резервируется место для данных с помощью следующих инструкций 

```MASM
mov ax, @data 
mov ds,ax
```

Потом вызываются инструкции для ввода размеров входной матрицы, `call input_width` и `call input_height` для столбцов и строк соответсвенно.
Блок инструкций `input_width` содержит в себе следующее: изначально на экран выводится текст (сообщение) о программе и ожидается ввод количества столбцов матрицы

```bash
; This program was created by Arthur Nersisyan for educational practice.
Enter matrix WIDTH(max 5).
```

```MASM
input_width proc
    mov ah,09h     ;09h вывод строки на экран
    lea dx,msg1	;перемещение строки в регистр
    int 21h  		;вызов прерывания
```

После нажатия любой клавиши на клавиатуре, скан код данной кнопки попадает в регистр `al`, поэтому значение нажатой кнопки можно оттуда получить, сравнивать с интересующими нам значениями и сделать условный переход. 

```MASM
iw_1:   
    mov ah,1
    int 21h
    cmp al,'1'
    jl iw_1
    cmp al,'5'
    jg iw_1    ;JL, JG for cmp signed int,
    sub al,'0'
    sub ah,ah
    mov cx,ax               
    ret  
input_width endp
```   

В результате в регистр `cx` попадает значение (число, количество столбцов матрицы (от 1 до 5)).
Аналогичным образом в регистр `bx` попадает число строк матрицы. 

```MASM
input_height proc
    mov ah,09h
    lea dx,msg2
    int 21h  
ih_1:   
    mov ah,1
    int 21h
    cmp al,'1'
    jl ih_1
    cmp al,'5'
    jg ih_1 
    sub al,'0'
    sub ah,ah
    mov bx,ax               
    ret  
input_height endp
```

Для начала ввода значений элементов матрицы нужно поместить в стек значения количества строк и столбцов матрицы:

```MASM
push bx	;помещаем данные о размерах матрицы в стек
push cx
lea di,matrix1
call input_matrix ;вызываем блок инструкций для ввода элементов
```

`di` – данные</br>
`cx` – количество столбцов</br>
`bx` – количество строк</br>

С помощью ниже представленных инструкций постепенно в программу вводится матрица, после ввода каждого элемента на экран выводится сообщение `«Enter number N»`. После ввода всей матрицы сообщение `«Enter number N»` больше не будет выводится на экран.

```MASM
input_matrix proc
    mov si,10
    sub ax,ax
    mov dl,cl
    mov dh,bl 
im_0:      
    push cx
    push di 
im_1:     
    push dx                   
    push di
    lea di,msg5_w 
    mov al,dl
    sub al,cl
    add al,'1'
    mov [di],al
    lea di,msg5_h 
    mov al,dh
    sub al,bl
    add al,'1'
    mov [di],al
    pop di    
    mov ah,09h
    lea dx,msg5
    int 21h
    pop dx 
    sub bp,bp      
im_2:        
im_3:   mov ah,1 
    int 21h 
    cmp al,0Dh
    je im_5
    cmp al,'0'
    jl im_3
    cmp al,'9'
    jg im_3
    push ax
    push dx
    mov ax,bp
    mul si 
    mov bp,ax
    pop dx
    pop ax
    sub al,'0'
    sub ah,ah
    add bp,ax
    mov [di],bp
    cmp bp,1000
    jl im_2
    jmp im_4 
im_5:
    cmp bp,10
    jl im_3
im_4:   
    add di,2     
    loop im_1
    pop di
    pop cx
    add di,6*2 
    dec bx
    jnz im_0 
    ret
input_matrix endp   
```

Для вывода матрицы на экран используются два блока инструкций: `output_matrix` и `output_number`. Сначала  выводится матрица введенная пользователем, потом сортируется и выводится отсортированная матрица. 

```MASM
lea di,matrix1
call output_matrix ;полный код в приложении 1
Передаем данные в регистр общего назначения и вызываем «функцию» сортировки матрицы.
push bx         ; bx – количество строк
push cx
lea di,matrix1  ; di – данные
call sort_matrix
pop cx
pop bx
     
sort_matrix proc
    dec bx 
    mov cx,bx
    cmp cx, 0
    je sm_ex 
sm_0:
    push cx
    push di
    mov bx, 1 ; был ли обмен?
sm_1:        
    mov ax,[di + 10]
    cmp [di + 10 + 12],ax
    jae sm_c
    
    mov si,di
    add si,6*2
    call xchange_lines ; меняем местами
    sub bx,bx         
sm_c:          
    add di,6*2
    loop sm_1 
    pop di
    pop cx
    cmp bx,0
    je sm_0
sm_ex:         
    ret
sort_matrix endp
```

После сортировки на экран выводится надпись `«Sorted matrix:»` и отсортированная матрица.

```MASM
mov ah,09h
lea dx,msg4
int 21h 
```

Вывод сортированной матрицы осуществляется тем же блоком инструкций, что и изначальная матрица.

```MASM
lea di,matrix1
call output_matrix
```

В конце программа выводит на экран сообщение `«Done.»` и завершает работу.

```MASM
mov ah,09h
lea dx,msg6 ; Done.
int 21h    
mov ax,4c00h ;выход из программы
int 21h   
```
